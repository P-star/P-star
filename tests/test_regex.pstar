SCENE main {
	/* Create a strange string */
	string test = "aaa   bbbb  \\[xxx";

	/* Check if a regular expression matches */
	if (test =~ /a\s*b/) {
		echo "Does match\n";
	}

	/* Check if a regular expression does not match */
	if (test !~ /ffff/) { 
		echo "Does not match\n";
	}

	/* Check regular expression with interpolated variable */
	string var = "b.";
	if (test =~ /aa\s+$var$var.+$/) {
		echo "Does match\n";
	}

	/* Check regular expression with interpolated quoted variable */
	string var_meta = '\[';
	if (test =~ /($var_meta)(.*)$/q) {
		echo "Does match\n";
	}

	/* Pust the matches in the regular expression into the matches-array */
	array<string> matches;
	test =~ /..|./g => matches;

	int i;
	for (i = 0; i < @matches-1; i++) {
		echo "Match $i: '" . matches[i] . "'\n";
	}

	/* Test regex text replacement */
	test = "1234123412341234";
	echo "Before: $test\n";

	string r = "n";
	if (test =~ s/(.)[24]/-$1$r-/g) {
		echo "Replacement succeeded\n";
	}

	echo "After: $test\n";

	return 0;
}
